apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.5"
}

project.afterEvaluate {
    tasks.create(name: "jacocoTestCoverage", type: JacocoReport) {

        group = "Reporting"
        description = "Generate JaCoCo coverage reports"

        reports {
            xml.enabled = true
            xml.destination = file("${buildDir}/reports/coverage.xml")
            html.enabled = true
            html.destination = file("${buildDir}/reports/coverage.html")
        }

        def excludes = [
                '**/R.class',
                '**/R$*.class',
                '**/BuildConfig.*',
                '**/Manifest*.*',
                '**/*$ViewInjector*.*',
                '**/*$ViewBinder*.*',
                '**/injection/components/*.*',
                '**/*Provider*.class',
                '**/*_MembersInjector.class',
                '**/*Lambda*.*',
                '**/*_Factory.class'
        ]
        def javaTree = fileTree(dir: "${buildDir}/classes/java", excludes: excludes)
        def kotlinTree = fileTree(dir: "${buildDir}/classes/kotlin", excludes: excludes)
        def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: excludes)
        def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: excludes)

        classDirectories.from([javaTree, kotlinTree, debugTree, kotlinDebugTree])

        sourceDirectories.from([
                "${project.projectDir}/src/main/java",
                "${project.projectDir}/src/main/kotlin"
        ])
        executionData.from(fileTree(dir: project.projectDir, includes: ['**/*.exec', '**/*.ec']))
    }
}
